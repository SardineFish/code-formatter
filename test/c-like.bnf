<syntax> ::= <extern-statement-sequence>
<extern-statement-sequence> ::= <extern-statement> <extern-statement-sequence-1> | ""
<extern-statement> ::= <include> | <var-def> ';' | <function-def>
<include> ::= 'include' <include-path>
<include-path> ::= 'include-path' | 'string'
<var-def> ::= <signed-type> <var-list>
<var-list> ::= <name> <var-init> <var-list-1>
<name> ::= 'id'
<signed-type> ::= 'unsigned' <type-ptr> | <type-ptr>
<type-ptr> ::= <type> <type-ptr-1>
<type> ::= 'void' | 'char' | 'short' | 'int' | 'long' | 'long long' | 'float' | 'double' | 'id'
<var-init> ::= '=' <expr> | ""
<function-def> ::= <signed-type> <name> '(' <params> ')' <block>
<params> ::= <param> <params-1> | ""
<param> ::= <signed-type> <name>
<body> ::= <statement> <body-1> | ""
<statement> ::= <if> | <for> | <while> | <do-while> | <switch> | <expr> ';' | <var-def> ';' | <return-statement> ';' | <break-statement> ';' | <continue-statement> ';'
<return-statement> ::= 'return' <return-statement-1>
<break-statement> ::= 'break'
<continue-statement> ::= 'continue'
<if> ::= 'if' '(' <expr> ')' <block-body> <else-if> <else>
<else-if> ::= 'else if' '(' <expr> ')' <block-body> <else-if-1> | ""
<else> ::= 'else' <block-body> | ""
<for> ::= 'for' '(' <for-statement-1> ';' <for-statement-2> ';' <for-statement-3> ')' <block-body>
<for-statement-1> ::= <expr> | <var-def>
<for-statement-2> ::= <expr>
<for-statement-3> ::= <expr>
<while> ::= 'while' '(' <expr> ')' <block-body>
<do-while> ::= 'do' <block-body> 'while' '(' <expr> ')' ';'
<switch> ::= 'switch' '(' <expr> ')' <switch-block>
<switch-block> ::= '{' <switch-body> '}'
<switch-body> ::= <switch-case> <switch-body-1> | <switch-default> <switch-body-2> | ""
<switch-case> ::= 'case' <const> ':' <body>
<switch-default> ::= 'default' ':' <body>
<block-body> ::= '{' <body> '}' | <statement>
<block> ::= '{' <body> '}'
<const> ::= <number> | 'string' | 'char'
<number> ::= 'number' | 'number-long' | 'number-float'
<expr> ::= <assign>
<assign> ::= <logic-term> <assign-1>
<assign-op> ::= '=' | '+=' | '-=' | '*=' | '/=' | '%' | '<<=' | '>>=' | '&=' | '^='
<logic-term> ::= <cpr-term> <logic-term-1>
<logic-op> ::= '&' | '^' | '|' | '&&' | '||'
<cpr-term> ::= <plus-term> <cpr-term-1>
<compare-op> ::= '>' | '<' | '>=' | '<=' | '==' | '!='
<plus-term> ::= <term> <plus-term-1>
<term> ::= <factor> <term-1>
<factor> ::= <const> | <prefix-obj>
<prefix-obj> ::= <prefix> <prefix-obj> | <postfix-obj>
<postfix-obj> ::= <obj> <postfix-obj-1>
<obj> ::= <func-call> | 'id' | '(' <expr> ')'
<func-call> ::= 'id' '(' <func-call-params> ')'
<func-call-params> ::= <func-call-param> <func-call-params-1> | ""
<func-call-param> ::= <expr>
<prefix> ::= '++' | '--' | '-' | '~' | '*' | '&' | '!' | '(' <signed-type> ')'
<postfix> ::= '++' | '--' | '[' <expr> ']' | '.' 'id' | '->'
<type-ptr-1> ::= '*' <type-ptr-1> | ""
<logic-term-1> ::= <logic-op> <cpr-term> <logic-term-1> | ""
<cpr-term-1> ::= <compare-op> <plus-term> <cpr-term-1> | ""
<plus-term-1> ::= '+' <term> <plus-term-1> | '-' <term> <plus-term-1> | ""
<term-1> ::= '*' <factor> <term-1> | '/' <factor> <term-1> | ""
<postfix-obj-1> ::= <postfix> <postfix-obj-1> | ""
<extern-statement-sequence-1> ::= <extern-statement-sequence> | ""
<var-list-1> ::= ',' <var-list> | ""
<params-1> ::= ',' <params> | ""
<body-1> ::= <body> | ""
<return-statement-1> ::= <expr> | ""
<else-if-1> ::= <else-if> | ""
<switch-body-1> ::= <switch-body> | ""
<switch-body-2> ::= <switch-body> | ""
<assign-1> ::= <assign-op> <assign> | ""
<func-call-params-1> ::= ',' <func-call-params> | ""
